# ----------------------------------------------------
# This file is generated by the Qt Visual Studio Add-in.
# ------------------------------------------------------

TEMPLATE = app
TARGET = CarrotQt5

QT += core widgets gui gamepad
DEFINES += NOMINMAX QT_DLL QT_WIDGETS_LIB
CONFIG += c++14 debug_and_release

INCLUDEPATH += $(BASS_DIR) \
    $(BASS_FX_DIR)/C \
    $(BASS_FX_DIR) \
    ./GeneratedFiles \
    .

win32 {
    INCLUDEPATH += $(SFML_DIR)/include
    LIBS += -L"$(SFML_DIR)/lib"
}

win32 || unix:!macx {
    LIBS += -L"$(BASS_DIR)/x64" \
    -L"$(BASS_FX_DIR)/x64"
}

macx {
    MAKE_LFLAGS += -F /System/Library/Frameworks/CoreFoundation.framework/

    INCLUDEPATH += /usr/local/include
    
    LIBS += -L"$(BASS_DIR)" \
    -L"$(BASS_FX_DIR)" \
    -L"/usr/local/lib" \
    -framework CoreFoundation
}

LIBS += -lbass \
    -lbass_fx \
    -lsfml-graphics \
    -lsfml-window \
    -lsfml-system

DEPENDPATH += .

UI_DIR += ./GeneratedFiles
RCC_DIR += ./GeneratedFiles

CONFIG(debug, debug|release) {
    DESTDIR = ./Debug
    OBJECTS_DIR = ./Debug
    INCLUDEPATH += ./GeneratedFiles/Debug
    MOC_DIR += ./GeneratedFiles/Debug

    DEFINES += CARROT_DEBUG
} else {
    DESTDIR = ./Release
    OBJECTS_DIR = ./Release
    INCLUDEPATH += ./GeneratedFiles/Release
    MOC_DIR += ./GeneratedFiles/Release
}

include(CarrotQt5.pri)
win32:RC_FILE = CarrotQt5.rc

# Allow the binary to look for .so files in the same folder as itself
# (in our case: libbass.so and libbass_fx.so)
unix:QMAKE_LFLAGS += '-Wl,-rpath,\'\$$ORIGIN\''
